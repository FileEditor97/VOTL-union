plugins {
	id 'java'
	// ShadowJar
	// https://mvnrepository.com/artifact/com.gradleup.shadow/shadow-gradle-plugin
	id 'com.gradleup.shadow' version '9.0.0-beta12'
}

// Getting version
apply from: 'version.gradle'

String version = loadVersion().getVersion()
String mainClassName = 'union.Main'

repositories {
	// Use Maven Central for resolving dependencies.
	mavenCentral()
}

dependencies {
	// JDA library and additions
	// https://mvnrepository.com/artifact/net.dv8tion/JDA
	implementation('net.dv8tion:JDA:5.3.2') {						// JDA library
		exclude module: 'opus-java'
	}
	// https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
	implementation 'ch.qos.logback:logback-classic:1.5.18'			// Logging (required by JDA)

	// Tools
	// https://mvnrepository.com/artifact/com.jayway.jsonpath/json-path
	implementation 'com.jayway.jsonpath:json-path:2.9.0'			// json path-er
	// https://mvnrepository.com/artifact/org.xerial/sqlite-jdbc
	implementation 'org.xerial:sqlite-jdbc:3.49.1.0'				// sqlite database
	// https://mvnrepository.com/artifact/org.mariadb.jdbc/mariadb-java-client
	implementation 'org.mariadb.jdbc:mariadb-java-client:3.5.3'		// mariaDB (MySQL) database
	// https://mvnrepository.com/artifact/org.jsoup/jsoup
	implementation 'org.jsoup:jsoup:1.19.1'							// HTML tools
	// https://mvnrepository.com/artifact/io.github.java-diff-utils/java-diff-utils
	implementation 'io.github.java-diff-utils:java-diff-utils:4.15' // Diff finder
	// https://mvnrepository.com/artifact/org.json/json
	implementation 'org.json:json:20250107'                         // JsonObject
	// https://mvnrepository.com/artifact/com.github.ben-manes.caffeine/caffeine
	implementation 'com.github.ben-manes.caffeine:caffeine:3.2.0'	// Cache

	// Metrics
    // https://mvnrepository.com/artifact/io.github.classgraph/classgraph
    implementation 'io.github.classgraph:classgraph:4.8.179'        // Class lookup
	// https://mvnrepository.com/artifact/org.knowm.xchart/xchart
	implementation 'org.knowm.xchart:xchart:3.8.8'					// Charts

	// Eval
	// https://mvnrepository.com/artifact/org.apache.groovy/groovy
	implementation 'org.apache.groovy:groovy:4.0.26'				// Groovy - code evaluation
}

java {
	sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
}

// jar's Manifest
tasks.jar {
	enabled = false
}

tasks.build {
	dependsOn(shadowJar)
}

// ShadowJar config
tasks.shadowJar {
	archiveBaseName = 'VOTL'
	archiveVersion = version
	archiveClassifier = ''
	minimize {
		exclude(dependency('ch.qos.logback:logback-classic:.*'))
		exclude(dependency('org.xerial:sqlite-jdbc:.*'))
		exclude(dependency('org.mariadb.jdbc:mariadb-java-client:.*'))
		exclude(dependency('com.github.ben-manes.caffeine:caffeine:.*'))
		exclude(dependency('org.apache.groovy:groovy:.*'))
	}

	exclude('META-INF/*.SF')
	exclude('META-INF/*.DSA')
	exclude('META-INF/*.RSA')
	exclude('META-INF/LICENSE*')
	exclude('META-INF/NOTICE*')
	exclude('META-INF/DEPENDENCIES')

	manifest {
		attributes 'Implementation-Title': 'VOTL-union bot',
				'Implementation-Version': version,
				'Implementation-Vendor': 'FiLe group',
				'Main-Class': mainClassName
	}
}

tasks.compileJava {
	options.encoding = 'UTF-8'
}